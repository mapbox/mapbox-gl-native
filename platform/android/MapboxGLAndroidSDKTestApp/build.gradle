apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.mapbox.mapboxsdk.testapp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        baseline file("lint-baseline-local.xml")
        checkAllWarnings true
        warningsAsErrors true
        disable 'MissingTranslation', 'GoogleAppIndexingWarning', 'UnpackedNativeCode', 'IconDipSize', 'TypographyQuotes'
        abortOnError false
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        maxProcessCount 8
        javaMaxHeapSize "2g"
        preDexLibraries true
    }
}

dependencies {
    implementation(project(':MapboxGLAndroidSDK'))

    // Support libraries
    implementation rootProject.ext.dep.supportAppcompatV7
    implementation rootProject.ext.dep.supportRecyclerView
    implementation rootProject.ext.dep.supportDesign

    // Leak Canary
    debugImplementation rootProject.ext.dep.leakCanaryDebug
    releaseImplementation rootProject.ext.dep.leakCanaryRelease

    // Timber
    implementation rootProject.ext.dep.timber

    // Mapbox Android Services (Java component)
    implementation(rootProject.ext.dep.mapboxJavaServices) {
        transitive = true
    }
    implementation rootProject.ext.dep.lost

    // Testing dependencies
    androidTestImplementation rootProject.ext.dep.supportAnnotations
    androidTestImplementation rootProject.ext.dep.testRunner
    androidTestImplementation rootProject.ext.dep.testRules
    androidTestImplementation rootProject.ext.dep.testEspressoCore
    androidTestImplementation rootProject.ext.dep.testEspressoIntents
}

apply from: 'gradle-make.gradle'
apply from: 'gradle-config.gradle'
apply from: 'gradle-checkstyle.gradle'
apply from: '../gradle-lint.gradle'


